scenario CalamityOnTheHorizon {
  TONKA_PORTRAIT = "raider"
  TONKAA_TITLE = "Warchief Tonkaa"
  ORMU_PORTRAIT = "warrior"
  ORMU_TITLE = "Ormu"
  KAGAALU_PORTRAIT = "kagaalu"
  KAGAALU_TITLE = "Kagaalu"
  WARRIORS_TO_TRAIN = 3

  OBJECTIVES = [
    trainWarriors: [
      id: "trainWarriors",
      title: "Muster for battle",
      image: "warrior",
      description: "Collect gold and train $WARRIORS_TO_TRAIN warriors.",
      state: ObjectiveStates.active
    ],
    investigateThunder: [
      id: "investigateThunder",
      title: "Ominous rumbling",
      image: "raider",
      description: "Take Tonkaa and two warriors to the western shore to investigate the source of the thunder.",
      state: ObjectiveStates.active
    ],
    findKagaalu: [
      id: "findKagaalu",
      title: "Rogash's favorite",
      image: "kagaalu",
      description: "Search for the great Sarcomancer, Kagaalu, north of your base camp.",
      state: ObjectiveStates.active
    ],
    goToSwamp: [
      id: "goToSwamp",
      title: "Safety in the bog",
      image: "totem",
      description: "Find a way to cross the river and reach the safety of Moheela swamp. Kagaalu must survive!",
      state: ObjectiveStates.active
    ],
    findCanoes: [
      id: "findCanoes",
      title: "Find canoes",
      image: "canoe",
      description: "(OPTIONAL) Find canoes to cross the river.",
      state: ObjectiveStates.active
    ]
  ]

  onStart {
    players = Game.players
    kagaaluPlayer = players.1
    startingLocations = Game.grid.startingLocations
    @claimStartingUnits(players.0, startingLocations.0, 3, ["thrall", "warrior", "tonkaa", "sanghoyCamp"])
    @claimStartingUnits(kagaaluPlayer, startingLocations.1, 2, ["kagaalu", "runestoneOfRogash", "totem"])
    for unit in Game.grid.allUnits.where(=> $0.type.id == "canoe") {
      unit.setOwner(kagaaluPlayer)
    }
    @claimStartingUnits(players.2, startingLocations.2, 4, ["settler", "rifleman", "ironclad", "barracks"])

    hostileSanghoyPlayer = players.3
    for unit in Game.grid.allUnits\
        .where(=> $0.owner.id == Game.nature.id && $0.type.id in ["thrall", "warrior", "raider", "totem", "sanghoyCamp", "warCouncil"]) {
      unit.setOwner(hostileSanghoyPlayer)
    }
  }

  claimStartingUnits(player, startingLocation, searchRadius, allowedTypes) {
    for unit in Game.unitsInRange(startingLocation, searchRadius) {
      if unit.type.id in allowedTypes {
        unit.setOwner(player)
      }
    }
  }

  campCoordinates => Game.currentPlayer.ownedUnits.first(=> $0.type.id == "sanghoyCamp").coordinates
  westernShoreCoordinates => Game.grid.cells.first(=> $0?.comment == "westernShore").coordinates
  swampCell => Game.grid.cells.first(=> $0?.comment == "swamp")

  playBombardmentSound {
    Game.playSound(campCoordinates(), "ironclad", SoundTypes.attack, true)
  }

  onFirstRender {
    Game.,positionCamera(campCoordinates()) \
      .,zoomCamera(8) \
      .wait(1.5)
    for _ in 2.times {
      playBombardmentSound()
    }
    Game.,wait(1.5) \
      .,showDialog(TONKA_PORTRAIT, TONKAA_TITLE, "What was that?")
    playBombardmentSound()
    Game.,showDialog(TONKA_PORTRAIT, TONKAA_TITLE, "The thunder seems to be coming from the shore, but the skies are clear?") \
      .,showDialog(TONKA_PORTRAIT, TONKAA_TITLE, "We should train more warriors and investigate. You mongrels, move out! My men need those provisions!") \
      .,showDialog("thrall", "thrall", "No, not the whip!") \
      .currentScenario.addObjective(OBJECTIVES.trainWarriors)
    Game.refreshState()
  }

  onCardPlay(card) {
    if Game.currentScenario.objectiveState(OBJECTIVES.trainWarriors.id) == ObjectiveStates.active \
        && card.type.id == "warrior" \
        && Game.currentPlayer.ownedUnits.count(=> $0.type.id == "warrior") >= WARRIORS_TO_TRAIN {
      Game.currentScenario.updateObjective(OBJECTIVES.trainWarriors.id, [state: ObjectiveStates.completed])
      playBombardmentSound()
      Game.,showDialog(TONKA_PORTRAIT, TONKAA_TITLE, "Waanru, Uim, with me! We need to go to the shore and see if our fishers are in danger!") \
          .,showDialog(TONKA_PORTRAIT, TONKAA_TITLE, "$ORMU_TITLE, you stay here and watch for intruders!") \
          .currentScenario.,addObjective(OBJECTIVES.investigateThunder) \
            .addRevealedCell(westernShoreCoordinates())
    }
  }

  onMove(unit, from, to) {
    if Game.currentPlayerIndex == 0 {
      if Game.currentScenario.objectiveState(OBJECTIVES.investigateThunder.id) == ObjectiveStates.active \
          && Game.grid.visibleCells(Game.currentPlayer).any(=> $0.content?.type?.id in ["rifleman", "settler", "ironclad"]) {
        Game.showDialog(TONKA_PORTRAIT, TONKAA_TITLE, "What the...?")
        playBombardmentSound()
        for unit in Game.unitsInRange(westernShoreCoordinates(), 8).where(=> $0.owner.id == Game.currentPlayer.id) {
          unit.destroy(unit)
        }
        Game.currentScenario.updateObjective(OBJECTIVES.investigateThunder.id, [state: ObjectiveStates.completed])
        Game.,refreshState() \
          .,wait(1.5) \
          .,positionCamera(campCoordinates()) \
          .,zoomCamera(7) \
          .,showDialog(ORMU_PORTRAIT, ORMU_TITLE, "Thunder again, and Tonkaa is yet to return...")
        playBombardmentSound()
        for cell in Game.grid.cellsAround(westernShoreCoordinates(), 4, Game.currentPlayer) {
          Game.currentScenario.addRevealedCell(cell.coordinates)
        }
        Game.,refreshState() \
          .,positionCamera(westernShoreCoordinates()) \
          .,zoomCamera(7) \
          .,wait(2) \
          .,showMessage("The outsiders are dangerous! Don't go there!") \
          .,wait(1.5) \
          .,positionCamera(campCoordinates()) \
          .,showDialog(ORMU_PORTRAIT, ORMU_TITLE, "I need to seek council of the great sarcomancer, Kagaalu! His totem lies north, near the edge of the jungle.") \
          .currentScenario.addObjective(OBJECTIVES.findKagaalu)
        Game.refreshState()
      } else if Game.currentScenario.objectiveState(OBJECTIVES.findKagaalu.id) == ObjectiveStates.active \
          && Game.grid.visibleCells(Game.currentPlayer).any(=> $0.content?.type?.id == "kagaalu") {
        Game.currentScenario.updateObjective(OBJECTIVES.findKagaalu.id, [state: ObjectiveStates.completed])
        Game.,refreshState() \
          .,showDialog(ORMU_PORTRAIT, ORMU_TITLE, "Great one! Thunders are echoing in the clear sky! Tonkaa went to investigate, but didn't return!") \
          .,showDialog(KAGAALU_PORTRAIT, KAGAALU_TITLE, "I know! Rogash has spoken to me. These are the outsiders that captured many islands already!") \
          .,showDialog(KAGAALU_PORTRAIT, KAGAALU_TITLE, "Our tribe is too weak to stop them. We must move and find shelter across the great river Dair.") \
          .,showDialog(ORMU_PORTRAIT, ORMU_TITLE, "But the far side of the river is controlled by hostile Sanghoy!") \
          .,showDialog(KAGAALU_PORTRAIT, KAGAALU_TITLE, "We will have to evade them and seek refuge in the swamp of Moheela.")
        kagaalu = Game.grid.allUnits.first(=> $0.type.id == "kagaalu")
        kagaalu.setOwner(Game.currentPlayer)
        Game.currentPlayer.setHero(kagaalu)
        Game.currentScenario.,addRevealedCell(swampCell().coordinates) \
          .,addObjective(OBJECTIVES.goToSwamp)
        Game.,refreshState() \
          .,showDialog(KAGAALU_PORTRAIT, KAGAALU_TITLE, "Crossing the river won't be easy, but I have hidden a few Canoes somwhere on the shore...") \
          .currentScenario.addObjective(OBJECTIVES.findCanoes)
        Game.refreshState()
      } else if Game.currentScenario.objectiveState(OBJECTIVES.findCanoes.id) == ObjectiveStates.active \
          && Game.grid.visibleCells(Game.currentPlayer).any(=> $0.content?.type?.id == "canoe") {
        for unit in Game.grid.allUnits.where(=> $0.type.id == "canoe") {
          unit.setOwner(Game.currentPlayer)
        }
        Game.showDialog(KAGAALU_PORTRAIT, KAGAALU_TITLE, "There are out canoes!")
        Game.currentScenario.updateObjective(OBJECTIVES.findCanoes.id, [state: ObjectiveStates.completed])
        Game.refreshState()
      }
    }
  }

  onStateChange {
    if Game.currentScenario.objectiveState(OBJECTIVES.goToSwamp.id) == ObjectiveStates.active {
      if !Game.grid.allUnits.any(=> $0.type.id == "kagaalu") {
        Game.showMessage("Kagaalu died!")
        Game.currentScenario.updateObjective(OBJECTIVES.goToSwamp.id, [state: ObjectiveStates.failed])
        Game.lose(Game.players.0)
      } else if Game.currentPlayerIndex == 0 && Game.grid.visibleCells(Game.currentPlayer).any(=> $0?.comment == "swamp") {
        Game.currentScenario.updateObjective(OBJECTIVES.goToSwamp.id, [state: ObjectiveStates.completed])
        Server.win(Game.currentPlayer)
      }
    }
  }

  checkVictory => null
}